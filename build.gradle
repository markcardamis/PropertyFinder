plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id "com.github.node-gradle.node" version "2.2.4"
}

apply plugin: 'io.spring.dependency-management'

node {
	download = true
	version = '14.15.0'
	npmVersion = '6.14.8'
}

group = 'com.majoapps'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '15'
targetCompatibility = '15'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
	all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

repositories {
	mavenCentral()
	jcenter()
	google()
	maven {
      url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'com.sipios:spring-search:0.2.0'
	implementation 'org.orbisgis:h2gis:1.5.0'
	implementation 'org.hibernate:hibernate-spatial:5.4.21.Final'
	implementation 'com.okta.spring:okta-spring-boot-starter:1.4.0'
	implementation 'org.modelmapper:modelmapper:1.1.0'
	implementation 'com.github.vladimir-bukhtoyarov:bucket4j-core:4.10.0'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean


task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
	args = [
            '--mode', 'production'
	]
}

processResources.dependsOn 'buildReactApp'
clean {
	delete file('node_modules')
	delete file('src/main/resources/static/dist')
}