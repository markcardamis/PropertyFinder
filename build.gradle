plugins {
	id 'org.springframework.boot' version '2.6.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "com.github.node-gradle.node" version "3.0.1"
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

node {
	download = true
	version = '17.3.0'
	npmVersion = '8.3.0'
}

group = 'com.majoapps'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '15'
targetCompatibility = '15'

jar {
	enabled = false
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
	all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

repositories {
	mavenCentral()
	google()
	maven {
      url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'com.sipios:spring-search:0.2.0'
	implementation 'org.orbisgis:h2gis:1.5.0'
	implementation 'org.hibernate:hibernate-spatial:5.5.8.Final'
	implementation 'com.okta.spring:okta-spring-boot-starter:2.1.2'
	implementation 'org.modelmapper:modelmapper:2.4.4'
	implementation 'com.github.vladimir-bukhtoyarov:bucket4j-core:6.3.0'
	compileOnly 'org.projectlombok:lombok:1.18.22'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
	runtimeOnly 'org.postgresql:postgresql:42.2.24.jre7'
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean


task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/.bin/webpack')
	args = [
            '--mode', 'production'
	]
}

processResources.dependsOn 'buildReactApp'
clean {
	delete file('node_modules')
	delete file('src/main/resources/static/dist')
}